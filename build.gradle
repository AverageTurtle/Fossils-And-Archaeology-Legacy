plugins {
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.gradle' version '[6.0.18,6.2)'
}

version = "1.0.0"
group = mod_id

jarJar.enable()

base {
	archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
	mappings channel: 'official', version: minecraft_version
	
	runs {
		configureEach {
			workingDirectory project.file('run')
			
			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}
	
		client {
		}

		server {
		}
		
		data {
			workingDirectory project.file('run')

			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
		}
	}
}

sourceSets.main.resources { 
	srcDir 'src/generated/resources' 
}

dependencies {
	minecraft "net.neoforged:forge:${minecraft_version}-${forge_version}"

	compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))
	compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
	runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"))

	implementation fg.deobf("com.github.willatendo:Simple-Library:${simple_library_version}")
	jarJar(group: 'com.github.willatendo', name: 'Simple-Library', version: "[v1.0.0,v2.0.0)")
}

repositories {
	maven { url = "https://maven.blamejared.com/" }
	maven {
		url "https://jitpack.io" 
	}
}

reobf {
	jarJar { }
}

tasks.jarJar.finalizedBy('reobfJarJar')

jar {
	manifest {
		attributes([
				"Specification-Title" : "Fossils Legacy",
				"Specification-Vendor" : "Willatendo",
				"Specification-Version" : "1",
				"Implementation-Title" : project.name,
				"Implementation-Version" : project.jar.archiveVersion,
				"Implementation-Vendor" : "Willatendo",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

jar.finalizedBy('reobfJar')