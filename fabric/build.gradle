plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

base {
    archivesName = "fossilslegacy-fabric"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${api_version}"

    modApi include("simplelibrary:simplelibrary-fabric:${simple_library_version}")

    api "fuzs.forgeconfigapiport:forgeconfigapiport-common-neoforgeapi:${forge_config_api_version}"
    api "fuzs.forgeconfigapiport:forgeconfigapiport-common-forgeapi:${forge_config_api_version}"
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forge_config_api_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

    compileOnly project(":common")

    modImplementation "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"
}

sourceSets {
    main {
        resources {
            srcDir("../common/src/generated/resources")
            exclude("../common/src/generated/resources/.cache")
        }
    }
}

loom {
    accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}
